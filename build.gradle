
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'idea'


// code checks
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'checkstyle'
apply plugin: "jacoco"


group = "com.damick"
archivesBaseName = "dropwizard-metrics-cloudwatch"
version = "0.3.0"


def sonatypeRepositoryUrl
def isRelease = false
//set build variables based on build type (release, continuous integration, development)
if (hasProperty("release")) {
  sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
  isRelease = true
} else if (hasProperty("snapshot")) {
  version += "-SNAPSHOT"
  isRelease = true
} else {
  version += "-SNAPSHOT"
  sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
  ext.ossrhUsername = ""
  ext.ossrhPassword = ""
}


task createWrapper(type: Wrapper) {
  description = "Create a gradle wrapper for you project"
  //gradleVersion = '2.6'
  gradleVersion = '4.0.1'
}


signing {
  if (isRelease) {
    sign configurations.archives
  }
}

/// Code checks
checkstyle {
  configFile = rootProject.file('gradle/config/checkstyle/checkstyle.xml')
}

// exclude the checkstyle, findbugsTest, pmdTest on test source..
//gradle.startParameter.excludedTaskNames += ["checkstyleTest", "findbugsTest", "pmdTest"]
gradle.startParameter.excludedTaskNames += ["findbugsTest", "pmdTest"]
jacoco {
  toolVersion = '0.7.2.201409121644'
}
tasks.check.dependsOn(jacocoTestReport)
////


// http://blog.joda.org/2014/02/turning-off-doclint-in-jdk-8-javadoc.html
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
}

allprojects {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
}

task docJar(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
}

artifacts { // define the artifacts for a service
    archives sourcesJar { classifier "sources" }
    archives docJar { classifier "javadoc" }
}


group = 'com.damick'
def vendor = 'Jeffrey Damick'

jar.doFirst {
  manifest {
      attributes 'Implementation-Title': "${archivesBaseName}",
                 'Implementation-Version': "${version}",
                 'Implementation-Vendor': "${vendor}",
                  provider: 'gradle'
  }
}



uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: sonatypeRepositoryUrl) {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }
      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'dropwizard-metrics-cloudwatch'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'Dropwizard Metrics Plugin for reporting to AWS CloudWatch.'
        url 'https://github.com/jdamick/dropwizard-metrics-cloudwatch'

        scm {
          connection 'scm:git:git://github.com/jdamick/dropwizard-metrics-cloudwatch.git'
          developerConnection 'scm:git:git@github.com:jdamick/dropwizard-metrics-cloudwatch.git'
          url 'https://github.com/jdamick/dropwizard-metrics-cloudwatch'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'jdamick'
            url 'https://github.com/jdamick'
            name 'Jeffrey Damick'
            email 'jeffrey.damick@gmail.com'
          }
        }
      }
    }
  }
}

repositories {
    mavenCentral()
}

if (!project.hasProperty('dropwizVer')) {
  ext.dropwizVer = '0.9.2'
}

ext.awsVer = '1.10.57'

dependencies {

  // dropwizard
  testCompile "io.dropwizard:dropwizard-testing:$dropwizVer"
  testCompile "org.assertj:assertj-core:1.7.0"
  
  compile "com.amazonaws:aws-java-sdk-cloudwatch:$awsVer"

  compile "io.dropwizard:dropwizard-metrics:$dropwizVer"
  compile "com.blacklocus:metrics-cloudwatch:0.4.0"

  testCompile 'junit:junit:4.12'
}

